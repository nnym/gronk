plugins {
    id("java-gradle-plugin")
    id("maven-publish")
}

group("net.auoeke")
version("0.3.0")

sourceSets {
    main {
        java {
            srcDir("source")
            exclude("**/*.gradle")
        }

        resources {
            srcDir("source")
            include("**/*.gradle")
        }
    }

    test {
        java.srcDir("test/source")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnlyApi("org.jetbrains.kotlin:kotlin-gradle-plugin")

    testImplementation("org.codehaus.groovy:groovy-nio")
    testImplementation("org.ow2.asm:asm")
    testImplementation("org.ow2.asm:asm-tree")
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testCompileOnly("org.jetbrains:annotations:latest.integration")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")

    configurations.all {
        dependencies.withType(ExternalDependency).all {
            version {
                prefer("+")
            }
        }
    }
}

java {
    withSourcesJar()
}

gradlePlugin {
    plugins {
        gronk {
            id = "gronk"
            implementationClass = "net.auoeke.gronk.Gronk"
        }
    }
}

test {
    dependsOn(cleanTest, pluginUnderTestMetadata)
    useJUnitPlatform()
}

publishing {
    publications.all {
        versionMapping {
            allVariants {
                fromResolutionResult()
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.auoeke.net"

            credentials {
                username = System.properties["maven.username"]
                password = System.properties["maven.password"]
            }
        }
    }
}
