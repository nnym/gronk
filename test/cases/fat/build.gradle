plugins {
    id("java")
    id("maven-publish")
    id("signing")
    id("org.jetbrains.kotlin.jvm").version("+")
    id("gronk")
    id("com.github.johnrengelman.shadow").version("latest.release")
}

group("net.auoeke.gronk.test")

javaVersion(17)
/* equivalent to
sourceCompatibility = 17
targetCompatibility = 17
*/

gronk {
    // Add net.auoeke:uncheck to every annotation processor configuration.
    // Add repository https://maven.auoeke.net and central Maven repository.
    uncheck()
}

repositories {
    maven("storage.googleapis.com/devan-maven")
    /* equivalent to
    maven {url = name = "https://storage.googleapis.com/devan-maven"}
    */
}

dependencies {
    implementation("net.auoeke:reflect")
    annotationProcessor("org.ow2.asm:asm-tree", "net.bytebuddy:byte-buddy-agent", "net.auoeke:reflect")
    /* equivalent to
    implementation("net.auoeke:reflect") {
        version {
            prefer("+")
        }
    }
    */
}

shadowJar {
    mergeManifests {
        include {manifest.mainAttributes.getValue("Agent-Class")?.startsWith("net.auoeke.reflect")}
    }
}

publishing {
    repositories {
        maven("maven.auoeke.net") {
            username(systemProperty("maven.username"))
            password(systemProperty("maven.password"))
        }
        /* equivalent to
        maven {
            url = "https://maven.auoeke.net"

            credentials {
                username = System.properties["maven.username"]
                password = System.properties["maven.password"]
            }
        }
        */
    }

/* implicit
    publications {
        maven(MavenPublication) {
            from(components.java)
        }

        withType(MavenPublication) {
            versionMapping {
                allVariants {
                    fromResolutionResult()
                }
            }
        }
    }
*/
}
